{
  parserClass     = 'com.pehrs.vespa.yql.plugin.YqlParser'
  parserUtilClass = "com.pehrs.vespa.yql.plugin.psi.YqlParserUtil"
  psiPackage      = 'com.pehrs.vespa.yql.plugin.psi'
  psiImplPackage  = 'com.pehrs.vespa.yql.plugin.psi.impl'

  elementTypeHolderClass = 'com.pehrs.vespa.yql.plugin.YqlElementTypes'
  elementTypeClass =       'com.pehrs.vespa.yql.plugin.YqlElementType'
  psiClassPrefix = "Yql"
  psiVisitorName = "YqlElementVisitor"

  psiImplUtilClass = 'com.pehrs.vespa.yql.plugin.psi.impl.YqlPsiImplUtils'
  tokenTypeClass = 'com.pehrs.vespa.yql.plugin.YqlTokenType'

  implements("value") = "com.pehrs.vespa.yql.plugin.psi.YqlElement"
  extends("value") = "com.pehrs.vespa.yql.plugin.psi.impl.YqlElementImpl"

    tokens = [
        L_CURLY='{'
        R_CURLY='}'
        L_BRACKET='['
        R_BRACKET=']'

        COMMA=','
        COLON=':'
        LINE_COMMENT='regexp://.*'
        // "/*" ([^*]|\*+[^*/])* (\*+"/")?
        BLOCK_COMMENT='regexp:/\*([^*]|\*+[^*/])*(\*+/)?'
        // else /\*(?:[^*]|\*[^/])*\*+/

        // unclosed string literal matches till the line's end
        // any escape sequences included, illegal escapes are indicated by SyntaxHighlighter
        // and YqlStringLiteralAnnotator
        DOUBLE_QUOTED_STRING="regexp:\"([^\\\"\r\n]|\\[^\r\n])*\"?"
        SINGLE_QUOTED_STRING="regexp:'([^\\\'\r\n]|\\[^\r\n])*'?"
//        STRING='regexp:"([^\\"\r\n]|\\([\\"/bfnrt]|u[a-fA-F0-9]{4}))*"?'

        NUMBER='regexp:-?(0|[1-9]\d*)(\.\d+)?([eE][+-]?\d*)?'
        TRUE='true'
        FALSE='false'
        NULL='null'
        IDENTIFIER="regexp:[[:jletterdigit:]~!()*\-./@\^<>=]+"
    ]

    extends("container|literal|reference_expression")=value
    extends("array|object")=container
    extends("string_literal|number_literal|boolean_literal|null_literal")=literal
    implements("property")=[
        "com.pehrs.vespa.yql.plugin.psi.YqlElement"
        "com.intellij.psi.PsiNamedElement"
    ]
}

// For compatibility we allow any value at root level (see YqlStandardComplianceAnnotator)
// Empty file is also allowed
yql ::= value*

object ::= '{' object_element* '}' {
  pin=1
  methods=[
    findProperty
    getPresentation
  ]
  mixin="com.pehrs.vespa.yql.plugin.psi.impl.YqlObjectMixin"
}

// Hackity-hack to parse array elements and properties even if separating commas are missing,
// TODO: Find out if there is any simpler way to do so in GrammarKit
private object_element ::= property (','|&'}') {
  recoverWhile = not_brace_or_next_value
  pin = 1
}

private property_value ::= value

property ::= property_name (':' property_value) {
  methods=[
    getName
    getNameElement
    getValue
    // suppress getValueList() accessor
    value=""
    getPresentation
  ]
  mixin="com.pehrs.vespa.yql.plugin.psi.impl.YqlPropertyMixin"
  pin(".*")=1
}

private property_name ::= literal | reference_expression


query_property ::= '"yql"' (':' query_statement) {
  methods=[
     getName
     getNameElement
     getValue
     // suppress getValueList() accessor
     value=""
     getPresentation
   ]
   mixin="com.pehrs.vespa.yql.plugin.psi.impl.YqlQueryPropertyMixin"
   pin(".*")=1
}

basic_keyword ::= 'select'| 'from'| 'where'| 'order by'| 'limit'| 'offset'| 'timeout'

// {targetHits:5}nearestNeighbor(embedding,q_embedding)
nearest_neighbor_statement ::= '{' string_literal ':' NUMBER '}nearestNeighbor(' string_literal COMMA string_literal ')'

query_statement ::= DOUBLE_QUOTE (basic_keyword)* DOUBLE_QUOTE {
  methods=[
    getPresentation
  ]
  pin=1
}

array ::= '[' array_element* ']' {
  methods=[
    getPresentation
  ]
  pin=1
}

private array_element ::= value (','|&']') {
  recoverWhile = not_bracket_or_next_value
  pin=1
}

string_literal ::= SINGLE_QUOTED_STRING | DOUBLE_QUOTED_STRING {
  methods=[
    getTextFragments
    getValue
    isPropertyName
    SINGLE_QUOTED_STRING=""
    DOUBLE_QUOTED_STRING=""
  ]
  mixin="com.pehrs.vespa.yql.plugin.psi.impl.YqlStringLiteralMixin"

}
number_literal ::= NUMBER {
  methods=[
    NUMBER=""
    getValue
  ]
}
boolean_literal ::= TRUE | FALSE {
  methods=[
    getValue
  ]
}
null_literal ::= NULL

literal ::= string_literal | number_literal | boolean_literal | null_literal {
  methods=[
    isQuotedString
  ]
  mixin="com.pehrs.vespa.yql.plugin.psi.impl.YqlLiteralMixin"
}

fake container ::=

reference_expression ::= IDENTIFIER {
  mixin="com.pehrs.vespa.yql.plugin.psi.impl.YqlReferenceLiteralMixin"
}

value ::= object | array | literal | reference_expression

// Recoveries
private not_bracket_or_next_value ::= !(']'|value)
private not_brace_or_next_value ::= !('}'|value)